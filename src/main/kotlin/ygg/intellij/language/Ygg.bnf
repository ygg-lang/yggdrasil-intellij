{
    parserClass = "ygg.intellij.language.parser.YggParser"
    parserUtilClass = "ygg.intellij.language.psi.YggParserExtension"

    psiClassPrefix = "Ygg"
    psiImplClassSuffix = "Node"
    psiPackage = "ygg.intellij.language.psi"
    psiImplPackage = "ygg.intellij.language.psi_node"

    elementTypeHolderClass = "ygg.intellij.language.psi.YggTypes"
    elementTypeClass = "ygg.intellij.language.psi.YggElementType"
    tokenTypeClass = "ygg.intellij.language.psi.YggTokenType"

    psiImplUtilClass = "ygg.intellij.language.psi_node.YggPsiExtension"

    tokens = [
        GRAMMAR = '@grammar'
        FRAGMENT = '@fragment'
        IMPORT = '@import'
        IGNORE = '@ignore'
        AS = "as"

        PARENTHESIS_L = '('
        PARENTHESIS_R = ')'
        BRACKET_L = '['
        BRACKET_R = ']'
        BRACE_L = '{'
        BRACE_R = '}'
        ACCENT = '^'
        ANGLE_L = '<'
        ANGLE_R = '>'
        QUOTATION = '"'
        ESCAPE = '\'
        TAGGED = '<-'

        SOFT_CONNECT = '~'
        CHOOSE='|'

        EQ = '='
        COLON = ':'
        SEMICOLON = ';'
        COMMA = ','
        DOLLAR = '$'
        DOT = '.'
        AT = '@'

        NOT='!'
        OPTIONAL='?'
        MANY1 = '+'
        MANY = '*'
    ]

    extends = "com.intellij.extapi.psi.ASTWrapperPsiElement"
}

Ygg ::= statements*;
// fake used for mixin
// pri used for syntax highlight
fake comment_doc ::= COMMENT_DOC;
private comment ::= COMMENT_LINE | COMMENT_BLOCK | COMMENT_DOC;

private statements ::=
      grammar_statement
    | ignore_statement
    | import_statement
    | macro_statement
    | rule_statement
    | SEMICOLON;
// =====================================================================================================================
ignore_statement ::= IGNORE (rule_symbol | <<paired ignore_inner>>);
private ignore_inner ::= [rule_symbol (COMMA rule_symbol)* [COMMA]];
// ---------------------------------------------------------------------------------------------------------------------
// @grammar ygg {"*.yg","*.ygg", "*.grammar"}
// @grammar ygg;
grammar_statement ::= GRAMMAR grammar_symbol [<<paired grammar_inner>>];
private grammar_inner ::= string (COMMA string)* [COMMA];
grammar_symbol ::= SYMBOL;
// ---------------------------------------------------------------------------------------------------------------------
// @import
import_statement ::= IMPORT string [rule_symbol | <<paired include_inner>>];
private include_inner ::= [rule_symbol (COMMA rule_symbol)* [COMMA]];
// ---------------------------------------------------------------------------------------------------------------------
// @macro("path")
macro_statement ::= AT SYMBOL <<parenthesis macro_arg COMMA>>;
macro_arg ::= value | macro_statement;
// =====================================================================================================================
rule_statement ::= SYMBOL EQ expr
expr ::= function_statement | value;
// ---------------------------------------------------------------------------------------------------------------------
// $function("path")
function_statement ::= DOLLAR rule_symbol <<parenthesis expr COMMA>>;
rule_symbol ::= SYMBOL;
// ---------------------------------------------------------------------------------------------------------------------
value ::= NULL | BOOLEAN | num | string | regex | table | regex_range;
type_hint ::= SYMBOL;
number_suffix ::= SYMBOL;
regex ::= REGEX_QUOTE char* REGEX_QUOTE regex_mode;
regex_mode ::= SYMBOL;
regex_range ::= REGEX_RANGE_L REGEX_CHARACTER* REGEX_RANGE_R;
// ---------------------------------------------------------------------------------------------------------------------
pair ::= symbol_path eq value;
symbol_path ::= key (DOT key)*;
key ::= string | key_symbol | INTEGER;
private eq ::= EQ | COLON;
key_symbol ::= SYMBOL;
// ------------------------------------
private num ::= [SIGN] (INTEGER | DECIMAL) [number_suffix] | [SIGN] BYTE;
// ------------------------------------
string ::= esc | STRING_SQ char* STRING_SQ | STRING_DQ char* STRING_DQ;
private char ::= CHARACTER | esc;
private esc ::= ESCAPE_UNICODE | ESCAPE_SPECIAL;
// ------------------------------------
table ::= [type_hint] <<paired table_inner>>;
private table_inner ::= [table_item (COMMA table_item)* [COMMA]];
private table_item ::= pair | value;
// ------------------------------------
meta parenthesis ::= PARENTHESIS_L [<<item>> (<<delimiter>> <<item>>)* [<<delimiter>>]] PARENTHESIS_R;

private meta paired ::=
    PARENTHESIS_L <<param>> PARENTHESIS_R
  | BRACKET_L <<param>> BRACKET_R
  | BRACE_L <<param>> BRACE_R
