{
    parserClass = "ygg.intellij.language.parser.YggParser"
    parserUtilClass = "com.intellij.lang.parser.GeneratedParserUtilBase"

    psiClassPrefix = "Ygg"
    psiImplClassSuffix = "Node"
    psiPackage = "ygg.intellij.language.psi"
    psiImplPackage = "ygg.intellij.language.psi_node"

    elementTypeHolderClass = "ygg.intellij.language.psi.YggTypes"
    elementTypeClass = "ygg.intellij.language.psi.YggElementType"
    tokenTypeClass = "ygg.intellij.language.psi.YggElementType"

    extends = "com.intellij.extapi.psi.ASTWrapperPsiElement"
    psiImplUtilClass = "ygg.intellij.language.psi_node.YggPsiExtension"

    tokens = [
        AS = "as"

        PARENTHESIS_L = '('
        PARENTHESIS_R = ')'
        BRACKET_L = '['
        BRACKET_R = ']'
        BRACE_L = '{'
        BRACE_R = '}'
        ACCENT = '^'
        ANGLE_L = '<'
        ANGLE_R = '>'
        QUOTATION = '"'
        ESCAPE = '\'
        TAGGED = '<-'
        TO = '->'

        SOFT_CONNECT = '~'
        CHOOSE='|'

        EQ = '='
        COLON = ':'
        SEMICOLON = ';'
        COMMA = ','
        DOLLAR = '$'
        DOT = '.'
        AT = '@'
        HYPHEN = '-'
        NOT='!'
        OPTIONAL='?'
        MANY1 = '+'
        MANY = '*'
    ]
}

Ygg ::= statements*;
// fake used for mixin
// pri used for syntax highlight
fake comment_doc ::= COMMENT_DOC;
private comment ::= COMMENT_LINE | COMMENT_BLOCK | COMMENT_DOC;

private statements ::=
      grammar_statement
    | export_statement
    | import_statement
    | macro_statement
    | macro_call
    | class_statement
    | union_statement
    | climb_statement
    | define_statement
    | SEMICOLON;
// ---------------------------------------------------------------------------------------------------------------------
// @grammar ygg {"*.yg","*.ygg", "*.grammar"}
// @grammar ygg;
grammar_statement ::= "grammar" identifier object {
    pin = identifier
    recoverWhile = end_brace
};
private end_brace ::= BRACE_R;
// ---------------------------------------------------------------------------------------------------------------------
// @import
export_statement ::= "codegen" identifier object {
    pin = identifier
    recoverWhile = end_brace
}
import_statement ::= KW_IMPORT string_literal [import_body] {
    pin = string_literal
    recoverWhile = end_brace
}
import_body      ::= BRACE_L [identifier (COMMA identifier)* [COMMA]] BRACE_R;
// ---------------------------------------------------------------------------------------------------------------------
// @macro("path")
macro_statement ::= AT SYMBOL <<parenthesis macro_arg COMMA>>;
macro_arg ::= value | macro_statement;
// =====================================================================================================================
// def modifiers rule
class_statement ::= modifiers KW_CLASS identifier [rule_type] rule_body {
//    pin = identifier
//    recoverWhile = end_brace
    mixin = "ygg.intellij.language.mixin.MixinClassStatement"
};
rule_body     ::= BRACE_L [CHOOSE] [rule_expr] BRACE_R {
    mixin = "ygg.intellij.language.mixin.MixinRuleBody"
}
rule_type     ::= (COLON | ARROW) identifier;
// =====================================================================================================================
union_statement ::= modifiers KW_UNION identifier [rule_type] rule_body {
//    pin = identifier
//    recoverWhile = end_brace
    mixin = "ygg.intellij.language.mixin.MixinUnionStatement"
};
// =====================================================================================================================
climb_statement ::= modifiers KW_CLIMB identifier [rule_type] rule_body {
//    pin = identifier
//    recoverWhile = end_brace
//    mixin = "ygg.intellij.language.mixin.MixinUnionStatement"
};
// =====================================================================================================================
define_statement ::= modifiers KW_DEFINE identifier define_parameters [rule_type] rule_body {
//    pin = identifier
//    recoverWhile = end_brace
    mixin = "ygg.intellij.language.mixin.MixinDefineStatement"
};
define_parameters ::= PARENTHESIS_L [define_pair (COMMA define_pair)* COMMA?] PARENTHESIS_R;
define_pair ::= identifier [EQ expr];
// =====================================================================================================================
rule_expr ::= rule_term (infix rule_term | rule_term)*;
rule_term ::= (prefix)* rule_atom (suffix| range)*
rule_atom ::=
    PARENTHESIS_L [CHOOSE] rule_expr PARENTHESIS_R | field_mark | branch_mark | function_call |
    num | string_literal | namepath | charset;
infix ::= CHOOSE | SOFT_CONNECT;
prefix ::= ACCENT | NOT;
suffix ::= OPTIONAL | MANY | MANY1;
// =====================================================================================================================
range ::= '{' range_start? ','? range_end? '}';
range_start ::= num;
range_end ::= num;
// ---------------------------------------------------------------------------------------------------------------------
branch_mark ::= HASH identifier;
field_mark ::= identifier COLON field_rhs;
field_rhs  ::= identifier | charset | string_literal;
// ---------------------------------------------------------------------------------------------------------------------
// @function("path")
function_call ::= AT identifier <<parenthesis expr COMMA>>;
expr ::= term (infix term | term)*;
term ::= (prefix)* atom (suffix)*
atom ::= PARENTHESIS_L [CHOOSE] expr PARENTHESIS_R | field_mark | function_call | value;
// ---------------------------------------------------------------------------------------------------------------------
macro_call  ::= HASH identifier [<<parenthesis atom COMMA>>];
// ---------------------------------------------------------------------------------------------------------------------
value ::= BOOLEAN | num | string_literal | table | namepath | charset;
type_hint ::= identifier;
number_suffix ::= identifier;
charset ::= REGEX_RANGE;
// ---------------------------------------------------------------------------------------------------------------------
pair ::= symbol_path eq value;
symbol_path ::= key (DOT key)*
key ::= string_literal | key_symbol | INTEGER;
private eq ::= EQ | COLON;
key_symbol ::= SYMBOL;
// ---------------------------------------------------------------------------------------------------------------------
private num ::= [SIGN] (INTEGER | DECIMAL) [number_suffix] | [SIGN] BYTE;
// ---------------------------------------------------------------------------------------------------------------------
string_literal ::= esc | STRING_SQ char* STRING_SQ | STRING_DQ char* STRING_DQ;
private char ::= CHARACTER | esc;
private esc ::= ESCAPE_UNICODE | ESCAPE_SPECIAL;
// ---------------------------------------------------------------------------------------------------------------------
table ::= [type_hint] <<paired table_inner>>;
private table_inner ::= [table_item (COMMA table_item)* [COMMA]];
private table_item ::= pair | value;
// ---------------------------------------------------------------------------------------------------------------------
object ::= BRACE_L (object_item [COMMA|SEMICOLON])* BRACE_R
object_item ::= object_key COLON value;
object_key ::= identifier | string_literal;
// ---------------------------------------------------------------------------------------------------------------------
modifiers  ::= identifier*;
namepath   ::= identifier ((DOUBLE_COLON|DOT) identifier)*
identifier ::= SYMBOL {
    mixin = "ygg.intellij.language.mixin.MixinIdentifier"
}
// ---------------------------------------------------------------------------------------------------------------------
meta parenthesis ::= PARENTHESIS_L [<<item>> (<<delimiter>> <<item>>)* <<delimiter>>?] PARENTHESIS_R;

private meta paired ::=
    PARENTHESIS_L <<param>> PARENTHESIS_R
  | BRACKET_L <<param>> BRACKET_R
  | BRACE_L <<param>> BRACE_R
